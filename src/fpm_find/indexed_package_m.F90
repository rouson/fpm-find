! Copyright (c) 2026, The Regents of the University of California
! Terms of use are as specified in LICENSE.txt

module indexed_package_m
  !! Define an abstraction for the fortran-lang package-index packages
  use julienne_m, only : string_t
  implicit none

  private
  public :: indexed_package_t

  type indexed_package_t
    !! Encapslate package-specific information from the fortran-lang package_index.yml file
    private
    character(len=:), allocatable :: name_, description_, categories_, tags_
    character(len=:), allocatable :: github_, gitlab_, url_ ! optional (zero length if not present)
    character(len=:), allocatable :: license_, version_     ! optional (zero length if not present)
    type(string_t)  , allocatable :: build_systems_(:)      ! optional (zero-length array if not present)
  contains
    procedure url
    procedure as_text
    procedure contains
  end type

  interface indexed_package_t

    pure module function construct_from_components( &
      name, description, categories, tags, license, version, github, gitlab, url, build_systems) result(indexed_package)
      !! Construct new indexed_package_t object from components
      implicit none
      character(len=*), intent(in) :: name, description, categories, tags
      character(len=*), intent(in), optional :: github, gitlab, url
      character(len=*), intent(in), optional :: license, version
      type(string_t)  , intent(in), optional :: build_systems(:)
      type(indexed_package_t) indexed_package 
    end function

    pure module function construct_from_strings(lines) result(indexed_package)
      !! Construct new indexed_package_t object from file lines passed as an array of string_t objects
      implicit none
      type(string_t), intent(in) :: lines(:)
      type(indexed_package_t) indexed_package 
    end function

    pure module function construct_from_characters(new_line_separated) result(indexed_package)
      !! Construct new indexed_package_t object from file lines passed as characters separated by new_line('')
      implicit none
      character(len=*), intent(in) :: new_line_separated
      type(indexed_package_t) indexed_package 
    end function

  end interface

  interface

    pure module function url(self) result(package_url)
      !! Result is self's Uniform Resource Locater (URL)
      implicit none
      class(indexed_package_t), intent(in) :: self
      character(len=:), allocatable :: package_url
    end function

    pure module function as_text(self) result(text)
      !! Result is a new-line-separated text rendering of self's entries.
      implicit none
      class(indexed_package_t), intent(in) :: self
      character(len=:), allocatable :: text
    end function

    pure module function contains(self, search_string, search_name, search_url, case_sensitive) result(match)
      !! Result is true if any of the package's entries contain search_string as a substring; false otherwise.
      !! search_name and search_url restrict the search to the package name or URL of the union of the two.
      !! case_sensitive toggles case sensitivity
      implicit none
      class(indexed_package_t), intent(in) :: self
      character(len=*), intent(in) :: search_string
      logical, intent(in) :: search_name, search_url, case_sensitive
      logical match
    end function

  end interface

end module indexed_package_m